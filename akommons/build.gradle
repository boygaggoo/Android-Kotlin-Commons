apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    artifactVersion = '0.0.4'
    artifactName = 'akommons'
    siteUrl = 'https://github.com/dewarder/Android-Kotlin-Commons'
    gitUrl = 'https://github.com/dewarder/Android-Kotlin-Commons.git'
}

version = artifactVersion
group = 'com.dewarder'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName artifactVersion
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:25.1.1'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name artifactName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dewarder'
                        name 'Artem Glugovsky'
                        email 'dewarder@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties['bintray.user']
    key = properties['bintray.apikey']
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = "$group:$artifactName"
        userOrg = 'dewarder'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = artifactVersion
            desc = 'Added extensions for orientation and SoftInput'
            released = new Date()
            vcsTag = "v$artifactVersion"
        }
    }
}